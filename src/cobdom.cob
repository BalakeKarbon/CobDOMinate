000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. COBDOM.
000300 ENVIRONMENT DIVISION.
000400 CONFIGURATION SECTION.
000500 SOURCE-COMPUTER. UNIX-LINUX.
000600 OBJECT-COMPUTER. WASM.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900 DATA DIVISION.
001000 FILE SECTION.
001100 WORKING-STORAGE SECTION.
001200 COPY './data/js.cpy'.
001300 LINKAGE SECTION.
001400 01 LS-VERSION PIC 99V99.
001500 01 LS-ELEMENT-VARIABLE PIC X(128).
001600 01 LS-ELEMENT-TYPE PIC X(128).
001700 01 LS-PARENT PIC X(128).
001800 01 LS-RETURN PIC S9.
001900 01 LS-HTML PIC X(256).
002000 PROCEDURE DIVISION.
002100 COBDOM-VERSION SECTION.
002200 ENTRY 'COBDOM-VERSION' USING BY REFERENCE LS-VERSION.
002300*I think by reference is redundant here.
002400   MOVE .01 TO LS-VERSION.
002500*  CALL 'emscripten_run_script_int' USING BY CONTENT WS-JS-TEST.
002600*  MOVE FUNCTION TRIM('div', TRAILING) TO WS-ELEMENT-TYPE.
002700*  DISPLAY WS-ELEMENT-TYPE '.'.
002800   GOBACK.
002900 COBDOM-CREATE-ELEMENT SECTION.
003000 ENTRY 'COBDOM-CREATE-ELEMENT' USING BY 
003100   REFERENCE LS-RETURN, LS-ELEMENT-VARIABLE,
003200   LS-ELEMENT-TYPE.
003300   STRING 
003400     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
003500     LS-ELEMENT-VARIABLE
003600     PART-A OF CREATE-ELEMENT OF WS-JS-COMMANDS
003700     LS-ELEMENT-TYPE
003800     PART-B OF CREATE-ELEMENT OF WS-JS-COMMANDS DELIMITED BY SPACE
003900     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
004000     WS-NULL-BYTE DELIMITED BY SIZE
004100*The null byte is needed because were passing C strings to
004200*emscripten's API.
004300   INTO WS-JS-COMMAND END-STRING.
004400   DISPLAY WS-JS-COMMAND.
004500   CALL 'emscripten_run_script_int' USING BY REFERENCE
004600     WS-JS-COMMAND RETURNING LS-RETURN.
004700   GOBACK.
004800 COBDOM-APPEND-CHILD SECTION.
004900 ENTRY 'COBDOM-APPEND-CHILD' USING BY REFERENCE
005000   LS-RETURN, LS-ELEMENT-VARIABLE, LS-PARENT.
005100   STRING
005200     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
005300     LS-PARENT
005400     PART-A OF APPEND-CHILD OF WS-JS-COMMANDS
005500     LS-ELEMENT-VARIABLE
005600     PART-B OF APPEND-CHILD OF WS-JS-COMMANDS DELIMITED BY SPACE
005700     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
005800     WS-NULL-BYTE DELIMITED BY SIZE
005900   INTO WS-JS-COMMAND END-STRING.
006000   CALL 'emscripten_run_script_int' USING BY REFERENCE
006100     WS-JS-COMMAND RETURNING LS-RETURN.
006200   GOBACK.
006300 COBDOM-REMOVE-CHILD SECTION.
006400 ENTRY 'COBDOM-REMOVE-CHILD' USING BY REFERENCE
006500   LS-ELEMENT-VARIABLE, LS-RETURN, LS-PARENT.
006600   STRING
006700     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
006800     LS-PARENT
006900     PART-A OF REMOVE-CHILD OF WS-JS-COMMANDS
007000     LS-ELEMENT-VARIABLE
007100     PART-B OF REMOVE-CHILD OF WS-JS-COMMANDS DELIMITED BY SPACE
007200     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
007300     WS-NULL-BYTE DELIMITED BY SIZE
007400   INTO WS-JS-COMMAND END-STRING.
007500   CALL 'emscripten_run_script_int' USING BY REFERENCE 
007600     WS-JS-COMMAND RETURNING LS-RETURN.
007700   GOBACK.
007800*Correct all of the above function delimiters.
007900 COBDOM-INNER-HTML SECTION.
008000 ENTRY 'COBDOM-INNER-HTML' USING BY REFERENCE
008100   LS-RETURN, LS-ELEMENT-VARIABLE, LS-HTML.
008200   STRING
008300     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
008400     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
008500     PART-A OF INNER-HTML OF WS-JS-COMMANDS DELIMITED BY SIZE
008600     LS-HTML DELIMITED BY WS-NULL-BYTE
008700     PART-B OF INNER-HTML OF WS-JS-COMMANDS DELIMITED BY SIZE
008800     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
008900     WS-NULL-BYTE DELIMITED BY SIZE
009000   INTO WS-JS-COMMAND END-STRING.
009100   CALL 'emscripten_run_script_int' USING BY REFERENCE 
009200     WS-JS-COMMAND RETURNING LS-RETURN.
009300   GOBACK.
009400*TO-DO put returns in these functions to verify success! This
009500*This will have to be done by wrapping scripts into a function
009600*with a try catch statement or something of the sort.
009700*COBDOM-INNER-HTML SECTION.
009800*ENTRY 'COBDOM-INNER-HTML' USING
009900*So here this gets crazy, since we are using eval, we can return
010000*the variable name, or we could store the objects in a structure
010100*and return its key/index, or we could create and add it relying
010200*only on the id.
