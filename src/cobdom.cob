000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. COBDOM.
000300 ENVIRONMENT DIVISION.
000400 CONFIGURATION SECTION.
000500 SOURCE-COMPUTER. UNIX-LINUX.
000600 OBJECT-COMPUTER. WASM.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900 DATA DIVISION.
001000 FILE SECTION.
001100 WORKING-STORAGE SECTION.
001200 COPY './data/js.cpy'.
001300 LINKAGE SECTION.
001400 01 LS-VERSION PIC 99V99.
001500 01 LS-ELEMENT-VARIABLE PIC X(128).
001600 01 LS-ELEMENT-TYPE PIC X(128).
001700 01 LS-PARENT PIC X(128).
001800 01 LS-RETURN PIC S9.
001900 01 LS-HTML PIC X(256).
002000 01 LS-FUNCTION PIC X(64).
002100 01 LS-GENERIC PIC X(256).
002200*TO-DO: You dont need all of these only the max number of arguments for a
002300*function number of storage areas are required.
002400 PROCEDURE DIVISION.
002500 COBDOM-INIT SECTION.
002600*ENTRY 'COBDOM-INIT' USING LS-RETURN.
002700*  CALL 'emscripten_run_script_int' USING BY REFERENCE 
002800*  WS-COB-INIT RETURNING LS-RETURN.
002900 COBDOM-VERSION SECTION.
003000 ENTRY 'COBDOM-VERSION' USING BY REFERENCE LS-VERSION.
003100*I think by reference is redundant here.
003200   MOVE .01 TO LS-VERSION.
003300*  CALL 'emscripten_run_script_int' USING BY CONTENT WS-JS-TEST.
003400*  MOVE FUNCTION TRIM('div', TRAILING) TO WS-ELEMENT-TYPE.
003500*  DISPLAY WS-ELEMENT-TYPE '.'.
003600   GOBACK.
003700 COBDOM-CREATE-ELEMENT SECTION.
003800 ENTRY 'COBDOM-CREATE-ELEMENT' USING BY 
003900   REFERENCE LS-RETURN, LS-ELEMENT-VARIABLE,
004000   LS-ELEMENT-TYPE.
004100   STRING 
004200     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
004300     LS-ELEMENT-VARIABLE
004400     PART-A OF CREATE-ELEMENT OF WS-JS-COMMANDS
004500     LS-ELEMENT-TYPE
004600     PART-B OF CREATE-ELEMENT OF WS-JS-COMMANDS DELIMITED BY SPACE
004700     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
004800     WS-NULL-BYTE DELIMITED BY SIZE
004900*The null byte is needed because were passing C strings to
005000*emscripten's API.
005100   INTO WS-JS-COMMAND END-STRING.
005200   CALL 'emscripten_run_script_int' USING BY REFERENCE
005300     WS-JS-COMMAND RETURNING LS-RETURN.
005400   GOBACK.
005500 COBDOM-APPEND-CHILD SECTION.
005600 ENTRY 'COBDOM-APPEND-CHILD' USING BY REFERENCE
005700   LS-RETURN, LS-ELEMENT-VARIABLE, LS-PARENT.
005800   STRING
005900     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
006000     LS-PARENT
006100     PART-A OF APPEND-CHILD OF WS-JS-COMMANDS
006200     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
006300     WS-END-PARENTHESIS DELIMITED BY SIZE
006400     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
006500     WS-NULL-BYTE DELIMITED BY SIZE
006600   INTO WS-JS-COMMAND END-STRING.
006700   CALL 'emscripten_run_script_int' USING BY REFERENCE
006800     WS-JS-COMMAND RETURNING LS-RETURN.
006900   GOBACK.
007000 COBDOM-REMOVE-CHILD SECTION.
007100 ENTRY 'COBDOM-REMOVE-CHILD' USING BY REFERENCE
007200   LS-ELEMENT-VARIABLE, LS-RETURN, LS-PARENT.
007300   STRING
007400     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
007500     LS-PARENT
007600     PART-A OF REMOVE-CHILD OF WS-JS-COMMANDS
007700     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
007800     WS-END-PARENTHESIS DELIMITED BY SIZE
007900     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
008000     WS-NULL-BYTE DELIMITED BY SIZE
008100   INTO WS-JS-COMMAND END-STRING.
008200   CALL 'emscripten_run_script_int' USING BY REFERENCE 
008300     WS-JS-COMMAND RETURNING LS-RETURN.
008400   GOBACK.
008500*Correct all of the above function delimiters.
008600 COBDOM-INNER-HTML SECTION.
008700 ENTRY 'COBDOM-INNER-HTML' USING BY REFERENCE
008800   LS-RETURN, LS-ELEMENT-VARIABLE, LS-HTML.
008900   STRING
009000     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
009100     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
009200     PART-A OF INNER-HTML OF WS-JS-COMMANDS DELIMITED BY SIZE
009300     LS-HTML DELIMITED BY WS-NULL-BYTE
009400     WS-DOUBLE-QUOTE DELIMITED BY SIZE
009500     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
009600     WS-NULL-BYTE DELIMITED BY SIZE
009700   INTO WS-JS-COMMAND END-STRING.
009800   CALL 'emscripten_run_script_int' USING BY REFERENCE 
009900     WS-JS-COMMAND RETURNING LS-RETURN.
010000   GOBACK.
010100*TO-DO: Practically uplicate the above to make a textContent
010200*procedure.
010300 COBDOM-ADD-EVENT-LISTENER SECTION.
010400 ENTRY 'COBDOM-ADD-EVENT-LISTENER' USING BY REFERENCE
010500   LS-RETURN, LS-ELEMENT-VARIABLE, LS-ELEMENT-TYPE, 
010600   LS-FUNCTION.
010700   STRING
010800     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
010900     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
011000     PART-A OF ADD-EVENT-LISTENER OF WS-JS-COMMANDS 
011100     DELIMITED BY SIZE
011200     LS-ELEMENT-TYPE DELIMITED BY SPACE
011300     PART-B OF ADD-EVENT-LISTENER OF WS-JS-COMMANDS 
011400     DELIMITED BY SIZE
011500     LS-FUNCTION DELIMITED BY WS-NULL-BYTE
011600*    WS-END-PARENTHESIS DELIMITED BY SIZE
011700     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
011800     WS-NULL-BYTE DELIMITED BY SIZE
011900   INTO WS-JS-COMMAND END-STRING.
012000   CALL 'emscripten_run_script_int' USING BY REFERENCE 
012100     WS-JS-COMMAND RETURNING LS-RETURN.
012200   GOBACK.
012300*TO-DO: Add a paragraph for setting class name, setting style,
012400*and perhaps setting the overall page style code/maybe set
012500*style by class name?
012600 COBDOM-CLASS-NAME SECTION.
012700 ENTRY 'COBDOM-CLASS-NAME' USING BY REFERENCE LS-RETURN,
012800   LS-ELEMENT-VARIABLE, LS-GENERIC.
012900   STRING
013000     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
013100     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
013200     PART-A OF CLASS-NAME OF WS-JS-COMMANDS DELIMITED BY SIZE
013300     LS-GENERIC DELIMITED BY SPACE
013400     WS-DOUBLE-QUOTE DELIMITED BY SIZE
013500     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
013600     WS-NULL-BYTE DELIMITED BY SIZE
013700   INTO WS-JS-COMMAND END-STRING.
013800   CALL 'emscripten_run_script_int' USING BY REFERENCE 
013900     WS-JS-COMMAND RETURNING LS-RETURN.
014000   GOBACK.
014100 COBDOM-STYLE SECTION.
014200 ENTRY 'COBDOM-STYLE' USING BY REFERENCE LS-RETURN,
014300   LS-ELEMENT-VARIABLE, LS-GENERIC.
014400   STRING
014500     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
014600     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
014700     PART-A OF STYLE OF WS-JS-COMMANDS DELIMITED BY SIZE
014800     LS-GENERIC DELIMITED BY WS-NULL-BYTE
014900     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
015000     WS-NULL-BYTE DELIMITED BY SIZE
015100   INTO WS-JS-COMMAND END-STRING.
015200   CALL 'emscripten_run_script_int' USING BY REFERENCE 
015300     WS-JS-COMMAND RETURNING LS-RETURN.
015400   GOBACK.
015500*TO-DO: Implement cookie's and post requests ETC.
015600 COBDOM-SET-COOKIE SECTION.
015700 ENTRY 'COBDOM-SET-COOKIE' USING BY REFERENCE LS-RETURN,
015800   LS-ELEMENT-VARIABLE, LS-GENERIC.
015800*Here we use LS-ELEMENT-VARIABLE even though it is a cookie key.
016000   STRING
016100     PART-A OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
016200     PART-A OF SET-COOKIE OF WS-JS-COMMANDS DELIMITED BY SIZE
016300     LS-ELEMENT-VARIABLE DELIMITED BY SPACE
016400     WS-EQUALS DELIMITED BY SIZE
016500     LS-GENERIC DELIMITED BY WS-NULL-BYTE
016600     WS-DOUBLE-QUOTE DELIMITED BY SIZE
016700     PART-B OF STATUS-WRAPPER OF WS-JS-COMMANDS DELIMITED BY SIZE
016800     WS-NULL-BYTE DELIMITED BY SIZE
016900   INTO WS-JS-COMMAND END-STRING.
017000   CALL 'emscripten_run_script_int' USING BY REFERENCE 
017100     WS-JS-COMMAND RETURNING LS-RETURN.
017200   GOBACK.
017300*This will have to be done by wrapping scripts into a function
017400*with a try catch statement or something of the sort.
017500*COBDOM-INNER-HTML SECTION.
017600*ENTRY 'COBDOM-INNER-HTML' USING
017700*So here this gets crazy, since we are using eval, we can return
017800*the variable name, or we could store the objects in a structure
017900*and return its key/index, or we could create and add it relying
018000*only on the id.
